apply plugin: 'java'
apply plugin: 'nexus'
apply plugin: 'eclipse'

group = 'com.trigonic'
version = '0.4-SNAPSHOT'

project.ext {
    springVersion = '3.1.+'
    joptSimpleVersion = '4.3'
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "org.springframework:spring-beans:${springVersion}"
    compile "org.springframework:spring-context:${springVersion}"
    compile "org.springframework:spring-core:${springVersion}"
    compile "net.sf.jopt-simple:jopt-simple:${joptSimpleVersion}"
    compile 'com.trigonic:spring-utils:0.6'
    
    testCompile 'junit:junit:4.+'

    testRuntime 'org.slf4j:slf4j-simple:1.6.+'
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives packageJavadoc
    archives packageSources
}

modifyPom {
    project {
        name 'spring-cmdline - Spring Command Line'
        packaging 'jar'
        description 'This library provides a Spring mechanism for launching a command-line application with options and operands.'
        url 'https://github.com/TrigonicSolutions/spring-cmdline'
        inceptionYear '2011'

        parent {
            groupId 'org.sonatype.oss'
            artifactId 'oss-parent'
            version '7'
        }
 
        scm {
            url 'scm:git://github.com/TrigonicSolutions/spring-cmdline.git'
            connection 'scm:git://github.com/TrigonicSolutions/spring-cmdline.git'
        }
 
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
 
        developers {
            developer {
                id 'akrueger'
                name 'Alan Krueger'
                email 'alan@trigonic.com'
                roles{
                    role 'Developer'
                }
                timezone '-6'
            }
        }
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.2'
}

